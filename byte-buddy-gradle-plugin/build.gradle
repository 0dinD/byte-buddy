apply plugin: 'java'
apply plugin: 'java-gradle-plugin'

def pom = new XmlSlurper().parse(file('./pom.xml'))

group = pom.parent.groupId.text().toString()
version = pom.parent.version.text().toString()

description = pom.description.text().toString()

sourceCompatibility = JavaVersion.VERSION_1_6
targetCompatibility = JavaVersion.VERSION_1_6

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile gradleApi()
    // As this Gradle build is triggered from Maven, the release plugin requires an explicit reference.
    if (Boolean.getBoolean('net.bytebuddy.misc.release')) {
        logger.info('Using local Byte Buddy dependency during release build')
        compile files("../byte-buddy/target/byte-buddy-${version}.jar")
    } else {
        compile group: group, name: 'byte-buddy', version: version
    }
    testCompile gradleTestKit()
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.10.19'
}

// The extras switch determines if the Maven build should create a javadoc jar.
if (Boolean.getBoolean('net.bytebuddy.misc.extras')) {
    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }
} else {
    task javadocJar {
        logger.info('javadoc is only generated if net.bytebuddy.test.extras is set to true')
    }
}

test {
    systemProperty('net.bytebuddy.test.version', version)
    systemProperty('net.bytebuddy.test.integration', Boolean.getBoolean('net.bytebuddy.test.integration'))
}
